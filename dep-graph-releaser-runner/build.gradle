buildscript {
    ext.publish_description = 'Contains different runners, they are the entry point for users.'
}

dependencies {
    compile prefixedProject('maven')
    compile prefixedProject('serialization')
    compile prefixedProject('gui')
    compile prefixedProject('manipulation')

    runtimeOnly "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"
    testCompile prefixedProject('maven').sourceSets.test.output
}

task dist {
    def outputDir = "${project.buildDir}/dist"
    def batAndShs = file("src/main/").listFiles().findAll{ it.name.endsWith(".bat") || it.name.endsWith(".sh") }
    outputs.dir outputDir
    inputs.files batAndShs
    inputs.files jar.outputs.files
    inputs.files sourceSets.main.runtimeClasspath

    doLast {
        copy {
            from batAndShs
            into outputDir
        }
        copy {
            from sourceSets.main.runtimeClasspath.asFileTree.findAll { it.name.endsWith(".jar") }
            from jar.outputs.files
            into "$outputDir/lib"
        }
    }
}

task distZip(type: Zip) {
    def outputDir = file("${project.buildDir}/distZip")
    inputs.files dist
    outputs.dir outputDir
    from dist
    destinationDir outputDir
}


def htmlDir = file("${rootProject.buildDir}/html")
def jsonFile = file("$htmlDir/release.json")

String getDefinedParam(String name, String represent) {
    def prop = findProperty(name)
    if(prop == null) {
        throw new IllegalArgumentException("you need to specify the $represent with -P$name")
    }
    return prop
}

task man {
    def commandArg = 'command'
    inputs.property(commandArg, findProperty(commandArg))
    inputs.files dist
    doLast {
        def command = findProperty(commandArg) != null ? ["-$commandArg=${findProperty(commandArg)}"] : []
        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'
            args = ['man'] + command
        }
    }
}

task json {
    def groupIdArg = 'gId'
    def artifactIdArg = 'aId'
    def updateDependencyJobArg = 'updateDependencyJob'
    def defaultUpdateJob = "dgr-updater"
    def remoteRegexArg = 'remoteRegex'
    def defaultRemoteRegex = "\".*#https://buildsystem.loewenfels.ch/jenkins\""
    def dryRunJobArg = 'dryRunJob'
    def defaultDryRunJob = "dgr-dry-run"
    def paramsArg = 'regexParams'
    def defaultParamsArg = ".*#branch.name=master"
    def disableRegexArg = 'disableRegex'
    def jobMappingArg = 'jobMapping'
    inputs.property(groupIdArg, findProperty(groupIdArg))
    inputs.property(artifactIdArg, findProperty(artifactIdArg))
    inputs.property(updateDependencyJobArg, findProperty(updateDependencyJobArg) ?: defaultUpdateJob)
    inputs.property(remoteRegexArg, findProperty(remoteRegexArg) ?: defaultRemoteRegex)
    inputs.property(dryRunJobArg, findProperty(dryRunJobArg) ?: defaultDryRunJob)
    inputs.property(paramsArg, findProperty(paramsArg) ?: defaultParamsArg)
    inputs.property(disableRegexArg, findProperty(disableRegexArg) ?: '^$')
    inputs.property(jobMappingArg, findProperty(jobMappingArg) ?: '')
    inputs.files dist
    outputs.file jsonFile
    doLast {
        def groupId = getDefinedParam(groupIdArg, "groupId")
        def artifactId = getDefinedParam(artifactIdArg, "artifactId")
        def updateDependencyJob = findProperty(updateDependencyJobArg) ?: defaultUpdateJob
        def remoteRegex = findProperty(remoteRegexArg) ?: defaultRemoteRegex
        def dryRunJob = findProperty(dryRunJobArg) ?: defaultDryRunJob
        def params = ["-$paramsArg=${findProperty(paramsArg) ?: defaultParamsArg}"]
        def disableRegex = findProperty(disableRegexArg) != null ? ["-$disableRegexArg=${findProperty(disableRegexArg)}"] : []
        def jobMapping = findProperty(jobMappingArg) != null ?
            ["-$jobMappingArg=${findProperty(jobMappingArg)}"] :
            ["-$jobMappingArg=" +
                 "ch.loewenfels.depgraph:dgr-1=apnoea-test-1|" +
                 "ch.loewenfels.depgraph:dgr-2=apnoea-test-2|" +
                 "ch.loewenfels.depgraph:dgr-3=apnoea-test-3|" +
                 "ch.loewenfels.depgraph:dgr-4=apnoea-test-4|" +
                 "ch.loewenfels.depgraph:dgr-5=apnoea-test-5"
            ]

        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'
            args = ['json', groupId, artifactId, "${rootProject.projectDir}/repos", jsonFile.absolutePath, updateDependencyJob, remoteRegex, dryRunJob] +
                params + disableRegex + jobMapping
        }
    }
}

task copyResources {
    inputs.files dist
    outputs.dir htmlDir
    doLast {
        mkdir(htmlDir)
        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'
            args = ['html', htmlDir.absolutePath]
        }
    }
}

task cleanHtmlDir(type: Delete){
    delete htmlDir
}

task html {
    dependsOn json
    dependsOn copyResources
    copyResources.mustRunAfter(json)
}

task releasable {
    inputs.files dist
    doLast {
        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'
            args = ['releasable', "${rootProject.projectDir}/repos"]
        }
    }
}

task dependents {
    def groupIdArg = 'gId'
    def artifactIdArg = 'aId'
    def formatArg = 'format'
    inputs.property(groupIdArg, findProperty(groupIdArg))
    inputs.property(artifactIdArg, findProperty(artifactIdArg))
    inputs.property(formatArg, findProperty(formatArg) ?: 'list')
    inputs.files dist
    doLast {
        def groupId = getDefinedParam(groupIdArg, "groupId")
        def artifactId = getDefinedParam(artifactIdArg, "artifactId")
        def format = findProperty(formatArg) != null ? ["-$formatArg=${findProperty(formatArg)}"] : []

        javaexec {
            workingDir = rootProject.projectDir
            classpath = fileTree("${project.buildDir}/dist/lib/")
            main = 'ch.loewenfels.depgraph.runner.Main'
            args = ['dependents', groupId, artifactId, "${rootProject.projectDir}/repos"] + format
        }
    }
}

task server {
    doLast {
        SimpleHttpFileServerFactory factory = new SimpleHttpFileServerFactory()
        factory.start(file("$rootProject.buildDir/html"), 8085)
        println("Server Started on 8085 CTRL+C to kill it")
        java.lang.Thread.sleep(Long.MAX_VALUE)
    }
}

task resourcesJar(type: Jar) {
    dependsOn cleanHtmlDir
    dependsOn copyResources
    copyResources.mustRunAfter(cleanHtmlDir)
    from htmlDir
    from "$projectDir/src/jenkins.pom"
    classifier 'resources'
}

ext.publish_artifacts += [resourcesJar, distZip]
